buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'rip.deadcode'
version '0.1.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'io.ratpack:ratpack-guice:1.5.2',
            'org.thymeleaf:thymeleaf:3.0.9.RELEASE',
            'javax.servlet:javax.servlet-api:3.1.0',
    )
    testCompile(
            'junit:junit:4.12',
            'com.google.truth:truth:0.39',
            'org.mockito:mockito-core:2.15.0',

            'org.slf4j:jul-to-slf4j:1.7.25', // Guava uses java.util.logging
            'ch.qos.logback:logback-classic:1.2.3',

            'io.ratpack:ratpack-test:1.5.2',
    )
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    pkg {
        repo = 'test'
        name = 'ratpack-thymeleaf3'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/minebreaker-tf/ratpack-thymeleaf3.git'
    }
    publications = ['bintrayPublication']
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
